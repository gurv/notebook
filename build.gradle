plugins {
    id 'org.asciidoctor.jvm.convert' version '2.4.0'
    id 'org.ajoberstar.git-publish' version '2.0.0'
    id "org.sonarqube" version "2.7.1"
}

repositories {
    jcenter()
}

group 'ru.gurv.notebook'
version '1.0-SNAPSHOT'

ext {
    repoPath = 'gurv/notebook'
}

asciidoctor {
    sources { include 'index.adoc' }
    baseDirFollowsSourceDir()
    attributes 'toc': 'left', 'imagesdir': 'images'
    asciidoctorj {
        modules {
            diagram.version '1.5.12'
        }
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "gurv-github"
        property "sonar.projectVersion", "${project.version}"
        property "sonar.sources", "src"
        property "sonar.links.ci", "https://travis-ci.org/gurv/notebook"
        property "sonar.links.scm", "https://github.com/gurv/notebook"
        property "sonar.links.issue", "https://github.com/gurv/notebook/issues"
    }
}

gitPublish {
    branch = 'gh-pages'
    contents {
        from ("${buildDir}/docs/asciidoc") {
            include '**/*'
            exclude '**/.asciidoctor'
        }
    }
    if (System.env.GRGIT_PASS) {
        repoUri = "https://github.com/${repoPath}.git"
    } else {
        repoUri = "git@github.com:${repoPath}.git"
    }
}

build.dependsOn asciidoctor
gitPublishPush.dependsOn build
defaultTasks 'build'
