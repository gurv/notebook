buildscript {
    repositories {
        jcenter()
    }
    
    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.6.0'
        classpath 'org.asciidoctor:asciidoctorj-diagram:1.5.12'
    }
}

plugins {
    id 'org.ajoberstar.git-publish' version '2.0.0'
}

group 'ru.gurv.notebook'
version '1.0-SNAPSHOT'

ext {
    repoPath = 'gurv/notebook'
}

apply plugin: 'org.asciidoctor.convert'

asciidoctor {
    attributes \
        'toc': 'left',
        'imagesdir': 'images'
    sources { include '*.adoc' }
    sourceDir 'content'
    outputDir 'build'
    requires 'asciidoctor-diagram'
}

build {
    dependsOn asciidoctor
}

gitPublish {
    branch = 'gh-pages'
    contents {
        from 'build/html5'
    }
    if (System.env.GRGIT_PASS) {
        repoUri = "https://github.com/${repoPath}.git"
    } else {
        repoUri = "git@github.com:${repoPath}.git"
    }
}

gitPublishPush.dependsOn build

defaultTasks 'build'
